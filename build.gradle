plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '3.0.6'
    id 'io.spring.dependency-management' version '1.1.0'
}

group = 'dev.francisco.briceno'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'mysql:mysql-connector-java:8.0.27'

    testImplementation 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
}

tasks.named('test') {
    finalizedBy jacocoTestReport
    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test
    reports {
        // Can consume xml for automation
        xml.required = true;
        // And html for visual
        html.required = true;
    }
    // Clean the report - don't look at coverage on dtos, configs or models
    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(
                    dir: it,
                    exclude: [
                            'dev/francisco/briceno/prisongame/domain/*',
                            'dev/francisco/briceno/prisongame/dto/*',
                            'dev/francisco/briceno/prisongame/repository/*',
                            'dev/francisco/briceno/prisongame/PrisonGameApplication.class', ])
        })
    }
}
